cmake_minimum_required(VERSION 3.10)

project(rviz_ogre_vendor)

find_package(ament_cmake REQUIRED)

set(PACKAGE_VERSION "1.0.0")

if(WIN32 AND NOT ${CMAKE_VERBOSE_MAKEFILE})
  set(should_log ON)  # prevent warnings in Windows CI
else()
  set(should_log OFF)
endif()

macro(build_ogre)
  set(extra_cmake_args)
  set(OGRE_C_FLAGS ${CMAKE_C_FLAGS})
  set(OGRE_CXX_FLAGS ${CMAKE_CXX_FLAGS})
  # standard library is important for linking, but the other cxx flags are not
  if(CMAKE_CXX_FLAGS MATCHES "-stdlib=libc\\+\\+")
    set(OGRE_CXX_FLAGS "${OGRE_CXX_FLAGS} -stdlib=libc++")
  endif()

  if(DEFINED CMAKE_BUILD_TYPE)
    list(APPEND extra_cmake_args -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE})
  endif()

  if(WIN32)
    set(OGRE_C_FLAGS "${OGRE_C_FLAGS} /w /EHsc")
    set(OGRE_CXX_FLAGS "${OGRE_CXX_FLAGS} /w /EHsc")
  elseif(APPLE)
    set(OGRE_CXX_FLAGS "${OGRE_CXX_FLAGS} -std=c++14 -stdlib=libc++ -w")
    list(APPEND extra_cmake_args "-DCMAKE_OSX_ARCHITECTURES='x86_64'")
  else()  # Linux
    set(OGRE_C_FLAGS "${OGRE_C_FLAGS} -w")
    # include Clang -Wno-everything to disable warnings in that build. GCC doesn't mind it
    set(OGRE_CXX_FLAGS "${OGRE_CXX_FLAGS} -std=c++14 -w -Wno-everything")
  endif()

  find_package(Patch REQUIRED)
  include(ExternalProject)
  ExternalProject_Add(ogre-v13.4.1
    URL https://github.com/OGRECave/ogre/archive/refs/tags/v13.4.1.zip
    URL_MD5 c9970e5a9faf823f417cd92a4872761b
    TIMEOUT 1200
    LOG_CONFIGURE ${should_log}
    LOG_BUILD ${should_log}
    CMAKE_ARGS
      ${extra_cmake_args}
      -Wno-dev
  )
endmacro()

build_ogre()

if(WIN32)
  ament_environment_hooks(env_hook/rviz_ogre_vendor_library_path.bat)
  set(ENV_VAR_NAME "PATH")
  set(ENV_VAR_VALUE "opt\\rviz_ogre_vendor\\bin")
else()
  ament_environment_hooks(env_hook/rviz_ogre_vendor_library_path.sh)
  if(APPLE)
    set(ENV_VAR_NAME "DYLD_LIBRARY_PATH")
  else()
    set(ENV_VAR_NAME "LD_LIBRARY_PATH")
  endif()
  set(ENV_VAR_VALUE "opt/rviz_ogre_vendor/lib")
endif()
ament_environment_hooks(env_hook/rviz_ogre_vendor_library_path.dsv.in)

if(BUILD_TESTING)
  find_package(ament_lint_auto REQUIRED)
  ament_lint_auto_find_test_dependencies()
endif()

ament_package(
  # CONFIG_EXTRAS "rviz_ogre_vendor-extras.cmake.in"
)
